version 0.1
module rbac

// TODO: do we need to distinguish between service
type user {}

type realm {
    relation user_grant: [Any role_binding]
}

type tenant {
    relation realm: [ExactlyOne realm]
    relation user_grant: [Any role_binding]
    relation member: [Any user]
}

type group {
    relation owner: [ExactlyOne tenant]
    relation member: [Any user or group.member]
}

type role {}

type role_binding { // Revisit cardinality based on clamping decisions
    relation subject: [Any user or group.member]
    relation granted: [Any role]
}

public type workspace {
    relation parent: [ExactlyOne workspace or tenant]
    relation user_grant: [Any role_binding]
}


public extension add_permission_with_legacy(legacy_perm, granular_perm) {
    type role {
        allow_duplicates relation `${legacy_perm}`: [bool]
        relation `${granular_perm}`: [bool]
    }

    type role_binding {
        relation `${granular_perm}`: subject and (granted.`${granular_perm}` or granted.`${legacy_perm}`)
    }

    type realm {
        relation `${granular_perm}`: user_grant.`${granular_perm}`
    }

    type tenant {
        relation `${granular_perm}`: user_grant.`${granular_perm}` or realm.`${granular_perm}`
    }

    type workspace {
        relation `${granular_perm}`: user_grant.`${granular_perm}` or parent.`${granular_perm}`
    }
}

public extension add_permission(workspace_perm) {
    type role {
        relation `${workspace_perm}`: [bool]
    }

    type role_binding {
        relation `${workspace_perm}`: subject and granted.`${workspace_perm}`
    }

    type realm {
        relation `${workspace_perm}`: user_grant.`${workspace_perm}`
    }

    type tenant {
        relation `${workspace_perm}`: user_grant.`${workspace_perm}` or realm.`${workspace_perm}`
    }

    type workspace {
        relation `${workspace_perm}`: user_grant.`${workspace_perm}` or parent.`${workspace_perm}`
    }
}